<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="MoveRelativeCommand" Id="{8bf7bf47-fa9a-05ca-2d4d-773cc4412da4}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK INTERNAL MoveRelativeCommand EXTENDS MoveCommand
VAR
	_fb : MC_MoveRelative;
	_distance : LREAL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{6d76350a-62e2-0e6c-3bff-e9f4d6429695}" />
    <Method Name="FB_init" Id="{32b99a7d-6c6c-0510-2ee3-f9c120ec2431}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	axis : Axis;
	distance : LREAL;
	velocity : LREAL;
	acceleration : LREAL;
	deceleration : LREAL;
	jerk : LREAL;
	bufferMode : AxisBufferMode;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_axis := axis;
_distance := distance;
_velocity := velocity;
_acceleration := acceleration;
_deceleration := deceleration;
_jerk := jerk;
_bufferMode := bufferMode;]]></ST>
      </Implementation>
    </Method>
    <Property Name="IsActive" Id="{337e3bb0-4a8c-057b-2ffb-a07243d62010}">
      <Declaration><![CDATA[PROPERTY PUBLIC IsActive : BOOL]]></Declaration>
      <Get Name="Get" Id="{85dfeb45-aeac-0e4c-0e9b-ce264528dbb7}">
        <Declaration><![CDATA[PUBLIC]]></Declaration>
        <Implementation>
          <ST><![CDATA[IsActive := _fb.Active;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnExecuting" Id="{d5103174-3358-09ff-31b1-ad8d2c68e508}">
      <Declaration><![CDATA[METHOD PROTECTED OnExecuting
VAR_INPUT
	stateControl : I_AsyncCommandStateControl;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_fb(
	Axis := _axis._InternalAxisRef,
	Execute := TRUE,
	Distance := _distance,
	Velocity := _velocity,
	Acceleration := _acceleration,
	Deceleration := _deceleration,
	Jerk := _jerk,
	BufferMode := _bufferMode
);

IF _fb.Busy THEN
	RETURN;
END_IF

_fb(Axis := _axis._InternalAxisRef, Execute := FALSE);

IF _fb.CommandAborted THEN
	THIS^.Abort();
	RETURN;
END_IF

IF _fb.Error THEN
	stateControl.Error(_fb.ErrorID);
	RETURN;
END_IF

stateControl.Next();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>