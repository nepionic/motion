<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="Mc2HomingCommand" Id="{f38489a8-f3d1-00fc-3cbc-19adabac713f}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK Mc2HomingCommand EXTENDS Mc2AxisCommand IMPLEMENTS HomingCommand
VAR
	_fb : MC_Home;
	_position : LREAL;
	_mode : HomingMode;
	_options : ST_HomingOptions;
	_cam : REFERENCE TO BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{4bb4f6c3-4a3a-0fb6-233f-542309b06994}" />
    <Method Name="FB_init" Id="{ff67dd2d-b00d-04e6-2c86-3829e8fa9558}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	axis : Axis;
	position : LREAL;
	mode : HomingMode;
	options : ST_HomingOptions;
	cam : REFERENCE TO BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_axis := axis;
_position := position;
_mode := mode;
_options := options;
IF __ISVALIDREF(cam) THEN
	_cam REF= cam;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExecuting" Id="{597b9919-284f-075e-0be6-5c0c7c583868}">
      <Declaration><![CDATA[METHOD PROTECTED OnExecuting
VAR_INPUT
	stateControl : I_AsyncCommandStateControl;
END_VAR
VAR_INST
	_dummy : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(_cam) THEN
	_cam REF= _dummy;
END_IF

_fb(
	Axis := _axis._InternalAxisRef,
	Execute := TRUE,
	Position := _position,
	HomingMode := _mode,
	Options := _options,
	bCalibrationCam := _cam
);

IF _fb.Busy THEN
	RETURN;
END_IF

_fb(Axis := _axis._InternalAxisRef, Execute := FALSE);

IF _fb.CommandAborted THEN
	THIS^.Abort();
	RETURN;
END_IF

IF _fb.Error THEN
	stateControl.Error(_fb.ErrorID);
	RETURN;
END_IF

stateControl.Next();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>