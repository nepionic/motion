<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MoveRelativeCommand" Id="{0489af38-80cf-48c1-b1e3-c03536e8b586}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK INTERNAL MoveRelativeCommand EXTENDS MoveCommand
VAR
	_fb : MC_MoveRelative;
	_distance : LREAL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{0880fa7c-2919-4030-88be-790fc93e651a}" />
    <Method Name="FB_init" Id="{b5d5a662-7f77-4804-aaf2-75cbd9df0dea}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	axis : I_Axis;
	distance : LREAL;
	velocity : LREAL;
	acceleration : LREAL;
	deceleration : LREAL;
	jerk : LREAL;
	bufferMode : AxisBufferMode;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_axis := axis;
_distance := distance;
_velocity := velocity;
_acceleration := acceleration;
_deceleration := deceleration;
_jerk := jerk;
_bufferMode := bufferMode;]]></ST>
      </Implementation>
    </Method>
    <Property Name="IsActive" Id="{9618920c-f007-036d-1c6a-bfa70b01cf8f}">
      <Declaration><![CDATA[PROPERTY PUBLIC IsActive : BOOL]]></Declaration>
      <Get Name="Get" Id="{7f4f650b-cc6e-03d8-3fc6-99dd063b2633}">
        <Declaration><![CDATA[PUBLIC]]></Declaration>
        <Implementation>
          <ST><![CDATA[IsActive := _fb.Active;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnExecuting" Id="{76e54b80-70e9-0652-246c-bdecebd012b6}">
      <Declaration><![CDATA[METHOD PROTECTED OnExecuting
VAR_INPUT
	stateControl : I_AsyncCommandStateControl;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_fb(
	Axis := _axis._InternalAxisRef,
	Execute := TRUE,
	Distance := _distance,
	Velocity := _velocity,
	Acceleration := _acceleration,
	Deceleration := _deceleration,
	Jerk := _jerk,
	BufferMode := _bufferMode
);

IF _fb.Busy THEN
	RETURN;
END_IF

_fb(Axis := _axis._InternalAxisRef, Execute := FALSE);

IF _fb.CommandAborted THEN
	THIS^.Abort();
	RETURN;
END_IF

IF _fb.Error THEN
	stateControl.Error(_fb.ErrorID);
	RETURN;
END_IF

stateControl.Next();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>