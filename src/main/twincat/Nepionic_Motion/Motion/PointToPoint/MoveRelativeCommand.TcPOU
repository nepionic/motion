<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="MoveRelativeCommand" Id="{bc7c6a4d-a0c8-0d41-03a3-a948f7228976}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK MoveRelativeCommand EXTENDS Mc2MotionCommand
VAR
	fb : Tc2_Mc2.Mc_MoveRelative;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Property Name="Active" Id="{a9b8eae2-6700-0533-1b8a-0916c137ba34}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC Active : BOOL]]></Declaration>
      <Get Name="Get" Id="{8f0336ad-4343-0c0f-1824-62b812a050e9}">
        <Declaration><![CDATA[PUBLIC]]></Declaration>
        <Implementation>
          <ST><![CDATA[Active := fb.Active;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CyclicCall" Id="{d50098f0-8e24-0880-1f5e-cd825d5252d2}">
      <Declaration><![CDATA[METHOD PUBLIC CyclicCall]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.CyclicCall();
fb(Axis := axis_ref);]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_Init" Id="{c7cd4b91-7cc8-0b85-1b2d-75a4dde70efb}">
      <Declaration><![CDATA[(*FB_Init is always available implicitly and it is used primarily for initialization.
The return value is not evaluated. For a specific influence, you can also declare the
methods explicitly and provide additional code there with the standard initialization
code. You can evaluate the return value.*)
METHOD FB_Init : BOOL
VAR_INPUT
	(* TRUE: the retain variables are initialized (reset warm / reset cold)*)
	bInitRetains : BOOL;
	(* TRUE: the instance will be copied to the copy code afterward (online change)*)
	bInCopyCode	: BOOL;
	axis : REFERENCE TO Mc2Axis;
	distance : LREAL;
	velocity : LREAL;
	acceleration : LREAL := 0;
	deceleration : LREAL := 0;
	jerk : LREAL := 0;
	buffer_mode	: MotionBufferMode := MotionBufferMode.Aborting;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[fb.Distance := distance;
fb.Velocity := velocity;
fb.Acceleration := acceleration;
fb.Deceleration := deceleration;
fb.Jerk := jerk;
fb.BufferMode := Mc2BufferModeConvertTo(buffer_mode);]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExecuting" Id="{a2022e99-73be-0a75-1a4d-0c6e26e23aed}">
      <Declaration><![CDATA[METHOD PROTECTED OnExecuting
VAR_INPUT
	state_control : AsyncCommandStateControl;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[fb.Execute := TRUE;

IF fb.Done THEN
	state_control.Next();
	fb.Execute := FALSE;
END_IF

IF fb.CommandAborted THEN
	Abort();
	fb.Execute := FALSE;
END_IF

IF fb.Error THEN
	state_control.Error(fb.ErrorId);
	fb.Execute := FALSE;
END_IF

]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>