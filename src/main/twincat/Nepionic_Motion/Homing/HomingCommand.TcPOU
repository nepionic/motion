<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="HomingCommand" Id="{9bba2a25-e66a-452c-a023-e03e9553dbaf}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK HomingCommand EXTENDS AxisCommand IMPLEMENTS I_HomingCommand
VAR
	_fb : MC_Home;
	_position : LREAL;
	_mode : HomingMode;
	_options : ST_HomingOptions;
	_cam : REFERENCE TO BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{213c6c5f-64f3-4d35-8e9a-de896e179b68}" />
    <Method Name="FB_init" Id="{4914a270-4765-45ef-89b4-84a56ae06580}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	axis : I_Axis;
	position : LREAL;
	mode : HomingMode;
	options : ST_HomingOptions;
	cam : REFERENCE TO BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_axis := axis;
_position := position;
_mode := mode;
_options := options;
IF __ISVALIDREF(cam) THEN
	_cam REF= cam;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExecuting" Id="{d0490b78-0323-0728-0690-3db3be233d14}">
      <Declaration><![CDATA[METHOD PROTECTED OnExecuting
VAR_INPUT
	stateControl : I_AsyncCommandStateControl;
END_VAR
VAR_INST
	_dummy : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT __ISVALIDREF(_cam) THEN
	_cam REF= _dummy;
END_IF

_fb(
	Axis := _axis._InternalAxisRef,
	Execute := TRUE,
	Position := _position,
	HomingMode := _mode,
	Options := _options,
	bCalibrationCam := _cam
);

IF _fb.Busy THEN
	RETURN;
END_IF

_fb(Axis := _axis._InternalAxisRef, Execute := FALSE);

IF _fb.CommandAborted THEN
	THIS^.Abort();
	RETURN;
END_IF

IF _fb.Error THEN
	stateControl.Error(_fb.ErrorID);
	RETURN;
END_IF

stateControl.Next();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>